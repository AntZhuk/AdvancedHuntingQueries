// This file contains 2 queries

// Specify host to search for logged in users
let HostName = "";
//-- Finds all users that are currently logged into the specified computer -- //
DeviceInfo
    | distinct DeviceName, LoggedOnUsers
    | where DeviceName contains HostName
    | extend CurrentUsers = parse_json(LoggedOnUsers)
    | project-away LoggedOnUsers, DeviceName
    | mv-apply CurrentUsers on (
        project User = CurrentUsers.UserName, SID = CurrentUsers.Sid
    )
    | extend Username = tostring(User), OnPremSid = tostring(SID)
    | join IdentityInfo on OnPremSid
    | extend FullName = strcat(GivenName, " ", Surname)
    | summarize Count = count() by Username, FullName, JobTitle, EmailAddress, AccountObjectId
    | project-away Count
    
// ENTER INFO ON USER(S) INTO THE BELOW LISTS
// needs minimum of 1 entry into 1 of the 4 lists
// you can have multiple per list and use all 4 lists simultaneously
let AzureAccountIDs = dynamic([]);
let Usernames = dynamic([]);
let EmailAddresses = dynamic(["user@example.com"]);
let SIDs = dynamic([]);
DeviceInfo
    | distinct DeviceName, LoggedOnUsers
    | project DeviceName, CurrentUsers = parse_json(LoggedOnUsers)
    | mv-apply CurrentUsers on (
        project DeviceName, User = CurrentUsers.UserName, SID = CurrentUsers.Sid
    )
    | project DeviceName, Username = tostring(User), OnPremSid = tostring(SID)
    | join IdentityInfo on OnPremSid
    | where AccountObjectId in (AzureAccountIDs)
         or Username in (Usernames)
         or EmailAddress in (EmailAddresses)
         or OnPremSid in (SIDs)
    | extend FullName = strcat(GivenName, " ", Surname)
    | summarize Count = count() by DeviceName, FullName, JobTitle, EmailAddress, Username, AccountObjectId
    | project-away Count
